
/** upload to jcenter **/

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//项目主页
def siteUrl = 'https://github.com/m-zylab/SketchyComponent'    // project homepage
//项目的版本控制地址
def gitUrl = 'https://github.com/m-zylab/SketchyComponent.git' // project git

//发布到组织名称名字，必须填写
group = properties.getProperty("group")
//发布到JCenter上的项目名字，必须填写
def libName = properties.getProperty("name")
// 版本号，下次更新是只需要更改版本号即可
version = properties.getProperty("version")
/** compile 'com.zylab:SketchyComponent:1.0.0' **/
archivesBaseName = properties.getProperty("name")

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
//    options.links "https://github.com/5a59/MockNet/tree/master/mylibrary/docs/javadoc"
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'SketchyComponent'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'zy5a59'
                        name 'zy5a59'
                        email 'zy5a59@outlook.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty("user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "SketchyComponent"
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = 'A set of sketchy style components.'
        //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}